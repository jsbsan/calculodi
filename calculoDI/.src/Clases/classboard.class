' Gambas class file

Property tamano As Integer
Private htamano As Integer
Public tablero As New Integer[11, 11] 'tablero maximo de 8x8 y 10x10
Public seleccionado As New Integer[2, 2] 'celda de tablero seleccionado

Public Sub _new(squares As Integer)

  inicio(squares)
  'tablero iniciado...

End

Public Sub inicio(squares As Integer)

  Dim x As Integer
  Dim y As Integer

  htamano = squares
  Constantes.squares = squares

  For x = 1 To squares
    For y = 1 To squares
      tablero[x, y] = Constantes.blank
    Next
  Next

  For y = 1 To Constantes.initialrows
    For x = 1 To squares
      If (x + y) Mod 2 <> 0 Then
        tablero[x, y] = Constantes.white

      Endif

      If (x + ((squares + 1) - y)) Mod 2 <> 0 Then
        tablero[x, squares + 1 - y] = Constantes.black

      Endif

    Next
  Next

End

Private Function tamano_Read() As Integer

  Return htamano

End

Private Sub tamano_Write(Value As Integer)

  htamano = value

End

Public Sub piezasDisponibles(turno As Integer) As Boolean

  Dim x As Integer
  Dim y As Integer

  For x = 1 To htamano
    For y = 1 To htamano
      If tablero[x, y] = turno Or tablero[x, y] = turno * Constantes.king Then
        Return True
      Endif
    Next
  Next
  Return False 'no hay piezas de ese turno

End

Public Sub copia(board As Classboard)

  Dim x As Integer, y As Integer

  For x = 0 To 10
    For y = 0 To 10
      tablero[x, y] = board.tablero[x, y]
    Next
  Next

End

Public Function escribe() As String

  Dim x As Integer, y As Integer
  Dim cadena As String

  cadena = "tablero\n"
  For x = 0 To 10
    For y = 0 To 10
      cadena &= Str$(tablero[x, y]) & "\n"
    Next
  Next

  Return cadena

End
